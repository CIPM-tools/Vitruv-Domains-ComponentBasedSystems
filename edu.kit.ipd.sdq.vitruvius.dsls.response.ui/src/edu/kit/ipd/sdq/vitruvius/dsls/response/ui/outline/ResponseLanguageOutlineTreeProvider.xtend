/*
 * generated by Xtext 2.9.0
 */
package edu.kit.ipd.sdq.vitruvius.dsls.response.ui.outline

import org.eclipse.xtext.ui.editor.outline.impl.DefaultOutlineTreeProvider
import org.eclipse.xtext.ui.editor.outline.impl.DocumentRootNode
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ResponseFile
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.Response
import static extension edu.kit.ipd.sdq.vitruvius.dsls.response.generator.ResponseLanguageGeneratorUtils.*;
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.Trigger
import static extension edu.kit.ipd.sdq.vitruvius.dsls.response.helper.EChangeHelper.*;
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.CodeBlock
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.Effects
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ResponseLanguagePackage
import org.eclipse.xtext.ui.editor.outline.impl.EStructuralFeatureNode
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ConcreteModelElementChange
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.PreconditionBlock
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.TargetChange
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ArbitraryTargetMetamodelInstanceUpdate
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.AtomicFeatureChange
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.AtomicRootObjectChange
import edu.kit.ipd.sdq.vitruvius.dsls.mirbase.mirBase.MetamodelImport
import edu.kit.ipd.sdq.vitruvius.dsls.mirbase.mirBase.MirBasePackage
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ConcreteTargetModelCreate
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ConcreteTargetModelUpdate

/**
 * Outline structure definition for a response file.
 *
 * @author Heiko Klare
 */
class ResponseLanguageOutlineTreeProvider extends DefaultOutlineTreeProvider {
	protected def void _createChildren(DocumentRootNode root, ResponseFile responseFile) {
		val importsNode = createEStructuralFeatureNode(root, responseFile, 
			MirBasePackage.Literals.MIR_BASE_FILE__METAMODEL_IMPORTS,
			imageDispatcher.invoke(responseFile), "imports", false);
		for (imp : responseFile.metamodelImports) {
			createChildren(importsNode, imp);
		}
		val responsesNode = createEStructuralFeatureNode(root, responseFile, 
			ResponseLanguagePackage.Literals.RESPONSE_FILE__RESPONSES,
			imageDispatcher.invoke(responseFile), "responses", false);
		for (response : responseFile.responses) {
			createChildren(responsesNode, response);
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, MetamodelImport imp) {
		val importNode = createEObjectNode(parentNode, imp);
		createEStructuralFeatureNode(importNode,
			imp, MirBasePackage.Literals.METAMODEL_IMPORT__PACKAGE,
			imageDispatcher.invoke(imp.package),
			imp.package.name, true);
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, Response response) {
		val responseNode = createEObjectNode(parentNode, response);
		if (response.documentation != null) {
			createEStructuralFeatureNode(responseNode, response,
				ResponseLanguagePackage.Literals.RESPONSE__DOCUMENTATION,
				imageDispatcher.invoke(response.documentation),
				"Documentation", true);
		}
		val triggerNode = createEStructuralFeatureNode(responseNode, response, 
			ResponseLanguagePackage.Literals.RESPONSE__TRIGGER,
			imageDispatcher.invoke(response.trigger), "Trigger", response.trigger != null);
		if (response.trigger != null) {
			createChildren(triggerNode, response.trigger);
		}
		if (response.trigger?.precondition != null) {
			createChildren(triggerNode, response.trigger.precondition)
		}
		val effectsNode = createEStructuralFeatureNode(responseNode, response, 
			ResponseLanguagePackage.Literals.RESPONSE__EFFECTS,
			imageDispatcher.invoke(response.effects), "Effects", response.effects != null);
		if (response.effects != null) {
			createChildren(effectsNode, response.effects);
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, PreconditionBlock preconditionBlock) {
		createEObjectNode(parentNode, preconditionBlock);
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, Trigger trigger) {
		createEObjectNode(parentNode, trigger);
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, AtomicFeatureChange event) {
		createEObjectNode(parentNode, event);
		if (event.changedFeature != null) {
			createEObjectNode(parentNode, event.changedFeature.element);
			if (event.changedFeature.feature != null) {
				createEObjectNode(parentNode, event.changedFeature.feature);
			}
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, AtomicRootObjectChange event) {
		createEObjectNode(parentNode, event);
		if (event.changedElement != null) {
			createEObjectNode(parentNode, event.changedElement.element);
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, Effects effects) {
		if (effects.targetChange != null) {
			var targetChangeIsLeaf = false;
			val targetChange = effects.targetChange;
			if (targetChange instanceof ConcreteTargetModelCreate) {
				targetChangeIsLeaf = targetChange.rootElement?.elementType?.element == null;
			} if (targetChange instanceof ConcreteTargetModelUpdate) {
				targetChangeIsLeaf = targetChange.identifyingElement?.elementType?.element == null;
			} else if (targetChange instanceof ArbitraryTargetMetamodelInstanceUpdate) {
				targetChangeIsLeaf = targetChange.metamodelReference?.model == null;
			}
			val targetChangeNode = createEStructuralFeatureNode(parentNode, effects, 
				ResponseLanguagePackage.Literals.EFFECTS__TARGET_CHANGE,
				imageDispatcher.invoke(effects.targetChange), "Target change", 
					targetChangeIsLeaf);
			createChildren(targetChangeNode, effects.targetChange);
		}
		createChildren(parentNode, effects.codeBlock);
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, ArbitraryTargetMetamodelInstanceUpdate targetChange) {
		if (targetChange.metamodelReference?.model != null) {
			createEObjectNode(parentNode, targetChange.metamodelReference.model);
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, ConcreteTargetModelCreate targetChange) {
		if (targetChange.rootElement.elementType.element != null) {
			createEObjectNode(parentNode, targetChange.rootElement.elementType.element);
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, ConcreteTargetModelUpdate targetChange) {
		if (targetChange.identifyingElement.elementType.element != null) {
			createEObjectNode(parentNode, targetChange.identifyingElement.elementType.element);
		}
	}
	
	protected def void _createChildren(EStructuralFeatureNode parentNode, CodeBlock codeBlock) {
		createEObjectNode(parentNode, codeBlock);
	}
	
	protected def Object _text(MetamodelImport imp) {
		return imp?.name;
	}
	
	protected def Object _text(Response response) {
		if (response.trigger != null) {
			return response.responseName.replace("ResponseTo", "");
		}
		return "No response trigger specified";
	}
	
	protected def Object _text(Trigger trigger) {
		return "There is no outline for this trigger";
	}
	
	protected def Object _text(AtomicFeatureChange event) {
		if (event.changedFeature?.element != null) {
			return event.generateEChange()?.name;
		} else {
			return "No changed element specified"
		}
	}
	
	protected def Object _text(AtomicRootObjectChange event) {
		if (event.changedElement?.element != null) {
			return event.generateEChange()?.name;
		} else {
			return "No changed element specified"
		}
	}
	
	protected def Object _text(PreconditionBlock preconditionBlock) {
		return "Precondition Block"
	}
	
	protected def Object _text(CodeBlock codeBlock) {
		return "Execution Block"
	}
	
	protected def boolean _isLeaf(PreconditionBlock compareBlock) {
		return true;
	}
	
	protected def boolean _isLeaf(CodeBlock codeBlock) {
		return true;
	}
	
	protected def boolean _isLeaf(TargetChange targetChange) {
		return true;
	}
	
	protected def boolean _isLeaf(ConcreteModelElementChange event) {
		return true;
	}
	
}
