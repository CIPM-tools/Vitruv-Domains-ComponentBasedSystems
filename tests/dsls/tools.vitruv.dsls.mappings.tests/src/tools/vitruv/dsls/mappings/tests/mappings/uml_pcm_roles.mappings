import "http://www.eclipse.org/uml2/5.0.0/UML" as uml 
import "http://palladiosimulator.org/PalladioComponentModel/5.2" as pcm
    	
mappings: umlXpcmRoles for UML and PCM
 
mapping RequiredRole {

	map(pcm::OperationRequiredRole role
		pcm::OperationInterface operationInterface
		abstract pcm::InterfaceProvidingRequiringEntity requiringEntity as pcm::BasicComponent
	)
	with{
		role in requiringEntity[requiredRoles_InterfaceRequiringEntity]
		operationInterface in role[requiredInterface__OperationRequiredRole]
	}
	and(uml::Property property
		uml::Parameter parameter
		uml::Class class
		uml::Interface interface
		uml::Operation operation
	)
	with{
		property in class[ownedAttribute]
		interface in property[type]		
		interface in parameter[type]
		parameter in operation[ownedParameter]
	}
	such that{
		observe attributes{
			pcm::OperationRequiredRole[entityName]
			uml::Property[name]
			uml::Parameter[name]
		}
	}

}

mapping ProvidedRole {
	map(pcm::OperationProvidedRole role
		pcm::OperationInterface operationInterface
		abstract pcm::InterfaceProvidingRequiringEntity requiringEntity as pcm::BasicComponent
	)
	with{
		role in requiringEntity[providedRoles_InterfaceProvidingEntity]
		operationInterface in role[providedInterface__OperationProvidedRole]
	}
	and(uml::InterfaceRealization interfaceRealization
		uml::Class class
		uml::Interface interface
	)
	with{
		interfaceRealization in class[interfaceRealization]
		interface in interfaceRealization[contract]
	}
	such that{
		observe attributes{
			pcm::OperationProvidedRole[entityName]
			uml::InterfaceRealization[name]
		}
	}
}