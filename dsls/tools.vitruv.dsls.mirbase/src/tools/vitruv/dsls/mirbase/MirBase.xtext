grammar tools.vitruv.dsls.mirbase.MirBase
	with org.eclipse.xtext.xbase.Xbase hidden(WS, SL_COMMENT)

generate mirBase "http://www.kit.edu/ipd/sdq/vitruvius/dsls/mirbase/MirBase"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

DummyEntryRule:
	MirBaseFile;

fragment MirBaseFile:
	(metamodelImports+=MetamodelImport)*;

MetamodelImport:
	'import' package=[ecore::EPackage|STRING] 'as' name=ValidID (useSimpleNames?='using simple names')?;

NamedJavaElement:
	type=JvmTypeReference 'as' name=ValidID;

fragment ModelElement returns ModelElement:
	element=[ecore::EClass|QualifiedName]
;

UnnamedModelElement returns ModelElement:
	ModelElement;

NamedModelElement returns NamedModelElement:
	ModelElement ('as' name=ValidID)?
;

ClassicallyNamedModelElement returns NamedModelElement:
	ModelElement name=ValidID
;

//fragment ModelElement:
//	element=[ecore::EClass|QualifiedName]
//;
//
//UnnamedModelElement:
//	ModelElement;
//		
//NamedModelElement:
//	ModelElement ('as' name=ValidID)?
//;
	
FeatureOfElement:
	element=[ecore::EClass|QualifiedName] '[' feature=[ecore::EStructuralFeature|ValidID] ']'
;
	
MetamodelReference:
	model=[MetamodelImport]
;