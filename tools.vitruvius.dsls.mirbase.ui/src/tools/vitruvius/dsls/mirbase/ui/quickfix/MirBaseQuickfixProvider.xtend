/*
 * generated by Xtext 2.10.0-SNAPSHOT
 */
package tools.vitruvius.dsls.mirbase.ui.quickfix

import tools.vitruvius.dsls.mirbase.mirBase.MetamodelImport
import tools.vitruvius.dsls.mirbase.validation.MirBaseValidator
import tools.vitruvius.framework.util.bridges.EclipseBridge
import org.eclipse.xtext.ui.editor.quickfix.Fix
import org.eclipse.xtext.ui.editor.quickfix.IssueResolutionAcceptor
import org.eclipse.xtext.validation.Issue
import org.eclipse.xtext.xbase.ui.quickfix.XbaseQuickfixProvider

import static tools.vitruvius.dsls.mirbase.validation.EclipsePluginHelper.*
import tools.vitruvius.dsls.common.VitruviusDslsCommonConstants

/**
 * Custom quickfixes.
 * 
 * See https://www.eclipse.org/Xtext/documentation/304_ide_concepts.html#quick-fixes
 */
class MirBaseQuickfixProvider extends XbaseQuickfixProvider {
	@Fix(MirBaseValidator.METAMODEL_IMPORT_DEPENDENCY_MISSING)
	def addDependencyToManifest(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(issue, 'Add dependency.', 'Add the dependency.', null) [ element, context |
			val metamodelImport = element as MetamodelImport

			val contributorName = EclipseBridge.getNameOfContributorOfExtension(
				"org.eclipse.emf.ecore.generated_package", "uri", metamodelImport.package.nsURI)

			val project = getProject(metamodelImport.eResource)
			if (!hasDependency(project, contributorName)) {
				addDependency(project, contributorName)
			}
		]
	}

	@Fix(MirBaseValidator.VITRUVIUS_DEPENDENCY_MISSING)
	def addVitruviusDependenciesToManifest(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(issue, 'Add all Vitruvius depdendencies.', 'Add all Vitruvius depdendencies.', null) [ element, context |
			val project = getProject(element.eResource)
			for (String dependency : VitruviusDslsCommonConstants.VITRUVIUS_DEPENDENCIES) {
				println(dependency)
				if (!hasDependency(project, dependency)) {
					addDependency(project, dependency)
				}
			}
		]
	}
}
