/*
 * generated by Xtext 2.9.0
 */
package edu.kit.ipd.sdq.vitruvius.dsls.response.jvmmodel

import com.google.inject.Inject
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ChangeEvent
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.CodeBlock
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.CompareBlock
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.ModelChangeEvent
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.Response
import java.util.ArrayList
import org.eclipse.emf.ecore.EClass
import org.eclipse.emf.ecore.EObject
import org.eclipse.xtext.common.types.JvmFormalParameter
import org.eclipse.xtext.common.types.JvmGenericType
import org.eclipse.xtext.common.types.JvmTypeReference
import org.eclipse.xtext.common.types.JvmVisibility
import org.eclipse.xtext.xbase.jvmmodel.AbstractModelInferrer
import org.eclipse.xtext.xbase.jvmmodel.IJvmDeclaredTypeAcceptor
import org.eclipse.xtext.xbase.jvmmodel.JvmTypesBuilder

import static edu.kit.ipd.sdq.vitruvius.dsls.response.generator.ResponseLanguageGeneratorConstants.*

import static extension edu.kit.ipd.sdq.vitruvius.dsls.response.helper.EChangeHelper.*
import static extension edu.kit.ipd.sdq.vitruvius.dsls.response.helper.ResponseLanguageHelper.*
import edu.kit.ipd.sdq.vitruvius.dsls.response.responseLanguage.FeatureOfElement

/**
 * <p>Infers a JVM model from the source model.</p> 
 *
 * <p>The JVM model should contain all elements that would appear in the Java code 
 * which is generated from the source model. Other models link against the JVM model rather than the source model.</p>     
 */
class ResponseLanguageJvmModelInferrer extends AbstractModelInferrer {
    /**
     * convenience API to build and initialize JVM types and their members.
     */
	@Inject extension JvmTypesBuilder
	
	/**
	 * The dispatch method {@code infer} is called for each instance of the
	 * given element's type that is contained in a resource.
	 * 
	 * @param element
	 *            the model to create one or more
	 *            {@link org.eclipse.xtext.common.types.JvmDeclaredType declared
	 *            types} from.
	 * @param acceptor
	 *            each created
	 *            {@link org.eclipse.xtext.common.types.JvmDeclaredType type}
	 *            without a container should be passed to the acceptor in order
	 *            get attached to the current resource. The acceptor's
	 *            {@link IJvmDeclaredTypeAcceptor#accept(org.eclipse.xtext.common.types.JvmDeclaredType)
	 *            accept(..)} method takes the constructed empty type for the
	 *            pre-indexing phase. This one is further initialized in the
	 *            indexing phase using the closure you pass to the returned
	 *            {@link org.eclipse.xtext.xbase.jvmmodel.IJvmDeclaredTypeAcceptor.IPostIndexingInitializing#initializeLater(org.eclipse.xtext.xbase.lib.Procedures.Procedure1)
	 *            initializeLater(..)}.
	 * @param isPreIndexingPhase
	 *            whether the method is called in a pre-indexing phase, i.e.
	 *            when the global index is not yet fully updated. You must not
	 *            rely on linking using the index if isPreIndexingPhase is
	 *            <code>true</code>.
	 */
	def dispatch void infer(CodeBlock codeBlock, IJvmDeclaredTypeAcceptor acceptor, boolean isPreIndexingPhase) {
		if (isPreIndexingPhase) {
			return;
		}
		val response = codeBlock.containingResponse;
		val methodParameters = createResponseParameters(response, codeBlock)
		
		acceptor.accept(response.toClass("Response")) [
			members += codeBlock.toMethod(RESPONSE_APPLY_METHOD_NAME, typeRef(Void.TYPE)) [applyMethod |
				applyMethod.parameters += methodParameters
				applyMethod.body = codeBlock.code]
			it.makeClassStatic(response)
		]
	}
		
	def dispatch void infer(CompareBlock compareBlock, IJvmDeclaredTypeAcceptor acceptor, boolean isPreIndexingPhase) {
		if (isPreIndexingPhase) {
			return;
		}
		val response = compareBlock.containingResponse;
		val methodParameters = createResponseParameters(response, compareBlock)
		
		acceptor.accept(response.toClass("Response")) [
			members += compareBlock.toMethod("determineAffectedModel", typeRef(Boolean.TYPE)) [applyMethod |
				applyMethod.parameters += methodParameters
				applyMethod.body = compareBlock.code]
			it.makeClassStatic(response)
		]
	}
	
	private def createResponseParameters(Response response, EObject blockContext) {
		val event = response?.trigger;
		var EClass change;
		var FeatureOfElement foe;
		if (event instanceof ModelChangeEvent) {
			change = event.change;
			foe = event.feature;
		} else if (event instanceof ChangeEvent) {
			if (event.feature.feature != null) {
				change = event.change.generateEChange(event.feature.feature);
			} else {
				change = event.change.generateEChange(event.feature.element);
			}
			foe = event.feature;
		}
		val methodParameters = <JvmFormalParameter>newArrayList();
		val changeTypeParameters = new ArrayList<String>();
		if (change?.name != null) {
			if (event instanceof ModelChangeEvent || event instanceof ChangeEvent) {
				changeTypeParameters += getGenericTypeParameterFQNOfChange(change, foe)
			}
			methodParameters += blockContext.generateChangeParameter(change.instanceTypeName, changeTypeParameters);
		}
		
		val affectedModel = response.effects.affectedModel;
		if (affectedModel != null) {
			methodParameters += blockContext.generateAffectedModelParameter(affectedModel.model.instanceTypeName);
		}
		methodParameters
	}
	
	
	private def void makeClassStatic(JvmGenericType type, EObject context) {
		type.members += context.toConstructor [
			visibility = JvmVisibility::PRIVATE
			body = ''''''
			documentation = "Private constructor since this class is static"
		]
	}
	
	def generateAffectedModelParameter(EObject context, String affectedModelClassName) {
		generateParameter(context, AFFECTED_MODEL_PARAMETER_NAME, affectedModelClassName);
	}
	
	def generateChangeParameter(EObject context, String changeClassName, String... typeParameterClassNames) {
		generateParameter(context, CHANGE_PARAMETER_NAME, changeClassName, typeParameterClassNames);
	}
	
	private def generateParameter(EObject context, String parameterName, String parameterClassName, String... typeParameterClassNames) {
		val typeParameters = new ArrayList<JvmTypeReference>(typeParameterClassNames.size);
		for (typeParameterClassName : typeParameterClassNames) {
			typeParameters.add(typeRef(typeParameterClassName));	
		}		
		val changeType = typeRef(parameterClassName, typeParameters)
		return context.toParameter(parameterName, changeType);
	}
	
}
