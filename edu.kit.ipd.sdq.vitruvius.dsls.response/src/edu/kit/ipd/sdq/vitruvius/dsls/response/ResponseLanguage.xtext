grammar edu.kit.ipd.sdq.vitruvius.dsls.response.ResponseLanguage 
	with org.eclipse.xtext.xbase.Xbase hidden(WS, SL_COMMENT)

generate responseLanguage "http://www.kit.edu/ipd/sdq/vitruvius/dsls/response/ResponseLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


ResponseFile:
	(metamodelImports += MetamodelImport)*
	(responses += Response)*
;

MetamodelImport:
	'import' package=[ecore::EPackage|STRING] 'as' name=ValidID;

Response:
	(documentation=ML_COMMENT)?
	'response:' (name=ValidID)?
	trigger=Trigger 
	effects=Effects;

Effects:
	{Effects}
	('effect:' targetChange=TargetChange)? 
	('execute:' codeBlock=CodeBlock)?;

TargetChange:
	ConcreteTargetModelRootChange | ArbitraryTargetMetamodelInstanceUpdate;

ConcreteTargetModelRootChange:
	ConcreteTargetModelRootUpdate | ConcreteTargetModelRootCreate | ConcreteTargetModelRootDelete;
	
ConcreteTargetModelRootUpdate:
	'update root' rootModelElement=ModelElement 'corresponding to' correspondenceSource=CorrespondenceSourceDeterminationBlock;
	
ConcreteTargetModelRootCreate:
	'create root' rootModelElement=ModelElement 'as' name=STRING;

ConcreteTargetModelRootDelete:
	'delete root' rootModelElement=ModelElement ('corresponding to' correspondenceSource=CorrespondenceSourceDeterminationBlock)?;

ArbitraryTargetMetamodelInstanceUpdate:
	'update any' metamodelReference=MetamodelReference;

ModelElement:
	modelElement=[ecore::EClass|QualifiedName];

CorrespondenceSourceDeterminationBlock:
	object=XBlockExpression;

PreconditionBlock:
	code=XBlockExpression;

CodeBlock:
	code=XBlockExpression;

// TODO dummy for the invariant violation
InvariantViolationEvent:
	'invariant' violation=[ecore::EClass|QualifiedName];

Trigger:
	'trigger:' (ModelChange | InvariantViolationEvent)
	('precondition:' precondition=PreconditionBlock)?;
	
ModelChange:
	ConcreteModelElementChange | ArbitraryModelElementChange;

ConcreteModelElementChange returns ConcreteModelElementChange:
	ModelElementChangeType 'of' changedObject=FeatureOfElement;

ModelElementChangeType returns ConcreteModelElementChange:
	'element change' {ConcreteModelElementChange} | 
	'element create' {ConcreteModelElementCreate} |
	'element update' {ConcreteModelElementUpdate} |
	'element delete' {ConcreteModelElementDelete};

ArbitraryModelElementChange:
	'model change' 'in' changedModel=MetamodelReference;
	
MetamodelReference:
	model=[MetamodelImport];

FeatureOfElement:
	element=[ecore::EClass|QualifiedName] ('[' feature=[ecore::EStructuralFeature|ValidID] ']')?;

	
