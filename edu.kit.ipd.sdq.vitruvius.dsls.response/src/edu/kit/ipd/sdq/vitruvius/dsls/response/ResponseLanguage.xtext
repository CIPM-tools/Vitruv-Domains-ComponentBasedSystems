grammar edu.kit.ipd.sdq.vitruvius.dsls.response.ResponseLanguage 
	with org.eclipse.xtext.xbase.Xbase hidden(WS, SL_COMMENT)

generate responseLanguage "http://www.kit.edu/ipd/sdq/vitruvius/dsls/response/ResponseLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


ResponseFile:
	(metamodelImports += MetamodelImport)*
	(responses += Response)*
;

MetamodelImport:
	'import' package=[ecore::EPackage|STRING] 'as' name=ValidID;

Response:
	(documentation=ML_COMMENT)?
	'response to' trigger=Trigger effects=Effects;
	
Effects:
	{Effects}
	(targetModel=TargetModel)? ('when' perModelPrecondition=CompareBlock)? ('executes' codeBlock=CodeBlock)?;

TargetModel:
	UpdatedModel | CreatedModel;

UpdatedModel:
	'updates' rootModelElement=ModelElement 'corresponding to' correspondenceSource=CorrespondenceSourceDeterminationBlock;
	
CreatedModel:
	'creates' rootModelElement=ModelElement 'as' name=STRING;

ModelElement:
	modelElement=[ecore::EClass|QualifiedName];

CorrespondenceSourceDeterminationBlock:
	object=XBlockExpression;

CompareBlock:
	code=XBlockExpression;

CodeBlock:
	code=XBlockExpression;

// TODO dummy for the invariant violation
InvariantViolationEvent:
	"invariant" violation=[ecore::EClass|QualifiedName];

Trigger:
	ModelChange | InvariantViolationEvent;
	
ModelChange:
	ConcreteModelElementChange | ArbitraryModelElementChange;

ConcreteModelElementChange returns ConcreteModelElementChange:
	ModelElementChangeType 'of' changedObject=FeatureOfElement;

ModelElementChangeType returns ConcreteModelElementChange:
	'element change' {ConcreteModelElementChange} | 
	'element create' {ConcreteModelElementCreate} |
	'element update' {ConcreteModelElementUpdate} |
	'element delete' {ConcreteModelElementDelete};

ArbitraryModelElementChange:
	'model change' 'in' changedmodel=MetamodelReference;
	
MetamodelReference:
	model=[MetamodelImport];

FeatureOfElement:
	element=[ecore::EClass|QualifiedName] ('[' feature=[ecore::EStructuralFeature|ValidID] ']')?;

	
