grammar edu.kit.ipd.sdq.vitruvius.dsls.response.ResponseLanguage
	with org.eclipse.xtext.xbase.Xbase

generate responseLanguage "http://www.kit.edu/ipd/sdq/vitruvius/dsls/response/ResponseLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ResponseFile:
	(metamodelImports += MetamodelImport)*
	(responses += Response)*
;

MetamodelImport:
	'import' package=[ecore::EPackage|STRING] 'as' name=ValidID;

Response:
	'response to' trigger=Trigger effects=Effects;
	
Effects:
	('affects' affectedModel=AffectedModel)? ('when' perModelPrecondition=CompareBlock)? 'executes' codeBlock=CodeBlock;

AffectedModel:
	model=[ecore::EClass|QualifiedName];

CompareBlock:
	code=XBlockExpression;

CodeBlock:
	code=XBlockExpression;

// TODO legacy implementation; remove when not needed any more
ModelChangeEvent:
	"old change" change=[ecore::EClass|QualifiedName]
	'of' feature=FeatureOfElement;

// TODO dummy for the invariant violation
InvariantViolationEvent:
	"invariant" violation=[ecore::EClass|QualifiedName];

Trigger:
	ModelChangeEvent | InvariantViolationEvent | ChangeEvent;

ChangeEvent:
	change=[ecore::EClass|QualifiedName] 'of' feature=FeatureOfElement;

FeatureOfElement:
	element=[ecore::EClass|QualifiedName] ('[' feature=[ecore::EStructuralFeature|ValidID] ']')?;
	