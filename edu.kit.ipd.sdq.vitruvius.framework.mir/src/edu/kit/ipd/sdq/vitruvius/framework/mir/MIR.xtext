grammar edu.kit.ipd.sdq.vitruvius.framework.mir.MIR
	with org.eclipse.xtext.xbase.Xbase

generate mIR "http://www.kit.edu/ipd/sdq/vitruvius/framework/mir/MIR"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

MIRFile:
	'generates' 'package' generatedPackage=QualifiedName
	('generates' 'type' generatedClass=QualifiedName)?
		
	(
		imports+=Import |
		mappings+=Mapping | 
		invariants+=Invariant |
		responses+=Response
	)*
;

Import:
	'import' 'package' package=[ecore::EPackage|STRING]
	'as' name=ValidID;


// Mappings
Mapping:
	'map' BaseMapping
;

BaseMapping returns Mapping:
	{BaseMapping}
	mappedElements+=NamedEClass
	'to'
	mappedElements+=NamedEClass
	('{'
		( ('when' whens += PredicateBlock)
		| ('with' withs += SubMapping)
		| ('where' wheres += PredicateBlock)
		)*
	'}')?
;

SubMapping returns Mapping:
	{SubMapping}
	mappedElements+=NamedFeature
	'to'
	mappedElements+=NamedFeature
	('{'
		( ('when' whens += PredicateBlock)
		| ('with' withs += SubMapping)
		| ('where' wheres += PredicateBlock)
		)*
	'}')?
;

NamedEClass returns MappableElement:
	{NamedEClass}
	representedEClass=[ecore::EClass|QualifiedName]
	('as' name=ValidID)?
;

NamedFeature returns MappableElement:
	{NamedFeature}
	containingNamedEClass=[NamedEClass] '.' representedFeature=[ecore::EStructuralFeature|ValidID]
	('as' name=ValidID)?
;

// Invariants
EClassParameter:
	type=[ecore::EClass|QualifiedName]
	name=ValidID
;

Invariant:
	'context' context=[ecore::EClass|QualifiedName]
	'inv' name=ValidID ( '('
		((params += EClassParameter)
		 (',' params += EClassParameter)*)?
	')'
	)? ':'
	predicate=PredicateBlock
;

PredicateBlock returns ecore::EObject:
	('xbase' XBlockExpression) |
	('ocl' OCLBlock)
;

OCLBlock:
	oclString=STRING
;

// Response
Response:
	'on'
	(action=ResponseAction 'of')?
	context=NamedEClass
	('restore' inv=[Invariant] 'by')?
	restoreAction=XBlockExpression
;

enum ResponseAction:
	ANY='any' |
	CREATE='create' |
	CHANGE='change'
;