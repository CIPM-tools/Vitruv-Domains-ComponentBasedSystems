grammar edu.kit.ipd.sdq.vitruvius.framework.mir.MIR
	with org.eclipse.xtext.xbase.Xbase

generate mIR "http://www.kit.edu/ipd/sdq/vitruvius/framework/mir/MIR"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

MIRFile:
	'generates' 'package' generatedPackage=QualifiedName
	('generates' 'type' generatedClass=QualifiedName)?
		
	(
		imports+=Import |
		mappings+=Mapping | 
		invariants+=Invariant |
		responses+=Response
	)*
;

Import:
	'import' 'package' package=[ecore::EPackage|STRING]
	'as' name=ValidID;


// Mappings
Mapping returns ClassMapping:
	'map' ClassMapping
;

ClassMapping:
	mappedElements+=NamedEClass
	'to'
	mappedElements+=NamedEClass
	('{'
		( ('when' whens += PredicateBlock)
		| ('with' withs += FeatureMapping)
		| ('where' wheres += ExpressionBlock)
		)*
	'}')?
;

FeatureMapping:
	mappedElements+=NamedFeature
	'to'
	mappedElements+=NamedFeature
	('{'
		( ('when' whens += PredicateBlock)
		| ('with' withs += FeatureMapping)
		| ('where' wheres += ExpressionBlock)
		)*
	'}')?
;

NamedEClass:
	representedEClass=[ecore::EClass|QualifiedName]
	'as' name=ValidID
;

ClassOrFeature returns Ref:
	{ClassOrFeature}
	'(' ((('class' containingNamedEClass=[NamedEClass]) |
     ('feature' containingNamedFeature=[NamedFeature])))
    ')'
;

NamedFeatureCall returns Ref:
	ClassOrFeature
	({NamedFeatureCall.ref=current} "."
		(
			tail=[ecore::EStructuralFeature|ValidID]
			("[" type=[ecore::EClassifier|QualifiedName] "]")?
		)
	)*
;

NamedFeature:
	feature=NamedFeatureCall
	'as'
	name=ValidID
;

// Invariants
EClassParameter:
	type=[ecore::EClass|QualifiedName]
	name=ValidID
;

Invariant:
	'context' context=[ecore::EClass|QualifiedName]
	'inv' name=ValidID ( '('
		((params += EClassParameter)
		 (',' params += EClassParameter)*)?
	')'
	)? ':'
	predicate=PredicateBlock
;

PredicateBlock returns ecore::EObject:
	('xbase' XBlockExpression) |
	('ocl' OCLBlock) |
	('java' JavaBlock)
;

ExpressionBlock returns ecore::EObject:
	('xbase' XBlockExpression) |
	('java' JavaBlock)
;

OCLBlock:
	oclString=STRING
;

JavaBlock:
	javaString=STRING
;

// Response
Response:
	'on'
	(action=ResponseAction 'of')?
	context=NamedEClass
	('restore' inv=[Invariant] 'by')?
	restoreAction=XBlockExpression
;

enum ResponseAction:
	ANY='any' |
	CREATE='create' |
	CHANGE='change'
;