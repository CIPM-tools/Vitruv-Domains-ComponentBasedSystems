module tools.vitruv.dsls.GenerateMirBase

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe.utils.DirectoryCleaner

var rootPath = ".."
var testRootPath = "${rootPath}/../../tests/dsls"
var projectName = "tools.vitruv.dsls.mirbase"
var basePath = "${rootPath}/${projectName}"
var testBasePath = "${testRootPath}/${projectName}"


Workflow {
	component = DirectoryCleaner {
    	directory = "${basePath}/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${basePath}.ui/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${basePath}.ide/src-gen"
    }

    component = DirectoryCleaner {
    	directory = "${testBasePath}.tests/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${testBasePath}.ui.tests/src-gen"
    }
    
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "${testBasePath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testBasePath}.ui.tests"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "tools.vitruv.dsls.mirbase.MirBase"
			fileExtensions = "mirbase"

			serializer = {
				generateStub = false
			}
			
			formatter = {
				generateStub = true
			}
			
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}
