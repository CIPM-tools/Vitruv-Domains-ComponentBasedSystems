grammar tools.vitruv.dsls.commonalities.CommonalitiesLanguage
	with org.eclipse.xtext.xbase.Xbase
	hidden(WS, ML_COMMENT, SL_COMMENT)

generate language "http://vitruv.tools/dsls/commonalities/language"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://vitruv.tools/dsls/commonalities/elements"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

CommonalityFile:
	concept=ConceptDeclaration
	commonality=CommonalityDeclaration
;

ConceptDeclaration:
	'concept' name=ID
;

CommonalityDeclaration:
	'commonality' name=ID '{'
		participations+=ParticipationDecl*
		(attributes+=AttributeDeclaration|references+=ReferenceDeclaration)*
	'}'
;



/******************
 * Participations *
 ******************/
// this rule’s sole purpose is to tell Xtext we want *subclass* Participation
// see https://eclipse.org/Xtext/documentation/301_grammarlanguage.html#feature-and-type-hierarchy-generation
ParticipationDecl returns Participation:
	ParticipationDeclaration;

ParticipationDeclaration:
	'with' (SimpleParticipationDeclaration | TupleParticipationDeclaration)
	('called' alias=QUOTED_NAME)?
	('whereat' condition=ParticipationConditionDeclaration | condition=ParticipationNoCondition);

SimpleParticipationDeclaration:
	participationClass=QualifiedParticipationClassDeclaration
;

TupleParticipationDeclaration:
	domain=[Domain|DomainReference] ':' '(' parts+=TupleParticipationDeclarationPart (','
	parts+=TupleParticipationDeclarationPart)* ')'
;

TupleParticipationDeclarationPart:
	SimpleTupleParticipationDeclarationPart | ParticipationRelationDeclaration
;

SimpleTupleParticipationDeclarationPart:
	participationClass=UnqualifiedParticipationClassDeclaration
;

ParticipationRelationDeclaration returns TupleParticipationDeclarationPart:
	SimpleParticipationRelationDeclaration | BracedParticipationRelationDeclartion
;

SimpleParticipationRelationDeclaration returns ParticipationRelationDeclaration:
	leftClasses+=UnqualifiedParticipationClassDeclaration operator=[jvmTypes::JvmDeclaredType]
	rightClasses+=UnqualifiedParticipationClassDeclaration
;

BracedParticipationRelationDeclartion returns ParticipationRelationDeclaration:
	'(' leftClasses+=UnqualifiedParticipationClassDeclaration (',' leftClasses+=UnqualifiedParticipationClassDeclaration)*
	operator=[jvmTypes::JvmDeclaredType]
	rightClasses+=UnqualifiedParticipationClassDeclaration (',' rightClasses+=UnqualifiedParticipationClassDeclaration)*
	')'
;

fragment Aliasable:
	('as' alias=QUOTED_NAME)?
;

QualifiedParticipationClassDeclaration returns ParticipationClass:
	superMetaclass=[Metaclass|QualifiedMetaclass] Aliasable
;

UnqualifiedParticipationClassDeclaration returns ParticipationClass:
	superMetaclass=[Metaclass|UnqualifiedMetaclass] Aliasable
;

ParticipationConditionDeclaration returns ParticipationCondition:
	{SpecialParticipationCondition}
;

ParticipationNoCondition returns ParticipationCondition:
	{ParticipationNoCondition}
;



/**************
 * Attributes *
 **************/
AttributeDeclaration:
	'has' name=ID '{'
		mappings+=AttributeMappingSpecifiation*
	'}'
;

AttributeMappingSpecifiation:
	('=' {AttributeEqualitySpecification} | '->' {AttributeSetSpecification} | '<-' {AttributeReadSpecification})
	attribute=[ParticipationAttribute|QualifiedParticipationMember]
;



/**************
 * Attributes *
 **************/
 ReferenceDeclaration:
 	'has' name=ID 'referencing' referenceType=[Metaclass|QualifiedMetaclass] '{'
 		mappings+=ReferenceMappingSpecification*
 	'}'
 ;
 
 ReferenceMappingSpecification:
 	('=' {ReferenceEqualitySpecification})
 	reference=[ParticipationReference|QualifiedParticipationMember]
 	('via' referenceTargets+=[ParticipationClass|QualifiedParticipationClass] (',' referenceTargets+=[ParticipationClass|QualifiedParticipationClass])*)?
 ;


/******************
 * Datatype Rules *
 ******************/
fragment QualifiedClasslike:
	ID ':' ID;
	
QualifiedMetaclass:
	QualifiedClasslike
;

QualifiedParticipationClass:
	QualifiedClasslike
;

DomainReference:
	ID
;

UnqualifiedMetaclass:
	ID
;

QualifiedParticipationMember:
	QualifiedClasslike '.' ID
;
	
	
	
/******************
 * Terminal Rules *
 ******************/
terminal QUOTED_NAME:
	("'" NAME "'") | ('"' NAME '"') | ("“" NAME "”") | ("»" NAME "«") | ("«" NAME "»") | ("„" NAME "“")
;

terminal fragment NAME:
	('a'..'z' | 'A'..'Z') ('a'..'Z' | 'A'..'Z' | '0'..'9' | '-' | '_')*
;
	
