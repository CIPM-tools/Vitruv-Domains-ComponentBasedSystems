grammar tools.vitruv.dsls.commonalities.CommonalitiesLanguage
	with org.eclipse.xtext.xbase.Xbase hidden(WS, SL_COMMENT)

generate language "http://vitruv.tools/dsls/commonalities/language"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://vitruv.tools/dsls/commonalities/elements"

CommonalityFile:
	concept = ConceptDeclaration
	commonality = CommonalityDeclaration
;

ConceptDeclaration:
	'concept' name=ID
;


/***********************
 * Concept Declaration *
 ***********************/
 
CommonalityDeclaration:
 	'commonality' name=ID '{'
 		participations += ParticipationDeclaration*
 		attributes += AttributeDeclaration*
 	'}'
;

DomainReference:
	domain=[Domain]
;

QualifiedMetaclass:
	ID ':' ID
;

QualifiedParticipationAttribute:
	QualifiedMetaclass '.' ID
;

ParticipationDeclaration returns Participation:
	'with' (SimpleParticipationClassDeclaration | TupleParticipationDeclaration) ('as' alias=ID)?
;

fragment SimpleParticipationClassDeclaration returns Participation:
	classes+=ParticipationClassDeclaration
;

fragment TupleParticipationDeclaration returns Participation:
	'(' classes+=ParticipationClassDeclaration (',' classes+=ParticipationClassDeclaration)* ')'
;

ParticipationClassDeclaration returns ParticipationClass:
	superMetaclass=[Metaclass|QualifiedMetaclass] ('called' alias=ID)?
;

AttributeDeclaration:
	'has' name=ValidID '{'
		mappings += AttributeMappingSpecifiation*
	'}'
;

AttributeMappingSpecifiation:
	AttributeEqualitySpecification
;


AttributeEqualitySpecification:
	'=' attribute = [ParticipationAttribute|QualifiedParticipationAttribute]
;
