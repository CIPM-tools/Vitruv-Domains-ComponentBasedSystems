grammar tools.vitruv.dsls.commonalities.CommonalitiesLanguage
	with org.eclipse.xtext.xbase.Xbase
	hidden(WS, ML_COMMENT, SL_COMMENT)

generate language "http://vitruv.tools/dsls/commonalities/language"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://vitruv.tools/dsls/commonalities/elements"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

CommonalityFile:
	concept=ConceptDeclaration
	commonality=CommonalityDeclaration
;

Domain:
	ConceptDeclaration
;

ConceptDeclaration:
	'concept' name=ID
;

Metaclass: 
	CommonalityDeclaration
;

CommonalityDeclaration:
	{CommonalityDeclaration}
	'commonality' name=ID '{'
		participations+=Participation*
		(attributes+=AttributeDeclaration|references+=ReferenceDeclaration)*
	'}'
;



/******************
 * Participations *
 ******************/
PackageLike:
	Participation
;

Participation:
	'with' (SimpleParticipationDeclaration | TupleParticipationDeclaration)
	('called' alias=QUOTED_NAME)?
	('whereat' condition=ParticipationConditionDeclaration | condition=ParticipationNoCondition)
;

SimpleParticipationDeclaration:
	participationClass=QualifiedParticipationClassDeclaration
;

TupleParticipationDeclaration:
	domain=[Domain|DomainReference] ':' '(' parts+=TupleParticipationDeclarationPart (','
	parts+=TupleParticipationDeclarationPart)* ')'
;

TupleParticipationDeclarationPart:
	SimpleTupleParticipationDeclarationPart | ParticipationRelationDeclaration
;

SimpleTupleParticipationDeclarationPart:
	participationClass=UnqualifiedParticipationClassDeclaration
;

ParticipationRelationDeclaration:
	SimpleParticipationRelationDeclaration | BracedParticipationRelationDeclartion
;

SimpleParticipationRelationDeclaration returns ParticipationRelationDeclaration:
	leftClasses+=UnqualifiedParticipationClassDeclaration operator=[jvmTypes::JvmDeclaredType]
	rightClasses+=UnqualifiedParticipationClassDeclaration
;

BracedParticipationRelationDeclartion returns ParticipationRelationDeclaration:
	'(' leftClasses+=UnqualifiedParticipationClassDeclaration (',' leftClasses+=UnqualifiedParticipationClassDeclaration)*
	operator=[jvmTypes::JvmDeclaredType]
	rightClasses+=UnqualifiedParticipationClassDeclaration (',' rightClasses+=UnqualifiedParticipationClassDeclaration)*
	')'
;

fragment Aliasable:
	('as' alias=QUOTED_NAME)?
;

ClassLike:
	ParticipationClass
;

ParticipationClass:
	QualifiedParticipationClassDeclaration | UnqualifiedParticipationClassDeclaration
;

QualifiedParticipationClassDeclaration returns ParticipationClass:
	superMetaclass=[Metaclass|QualifiedMetaclass] Aliasable
;

UnqualifiedParticipationClassDeclaration returns ParticipationClass:
	superMetaclass=[Metaclass|UnqualifiedMetaclass] Aliasable
;

ParticipationConditionDeclaration returns ParticipationCondition:
	{SpecialParticipationCondition}
;

ParticipationNoCondition returns ParticipationCondition:
	{ParticipationNoCondition}
;



/**************
 * Attributes *
 **************/
Attribute:
	AttributeDeclaration | ParticipationAttribute
;
 
AttributeDeclaration:
	'has' name=ID '{'
		mappings+=AttributeMappingSpecifiation*
	'}'
;

AttributeMappingSpecifiation:
	('=' {AttributeEqualitySpecification} | '->' {AttributeSetSpecification} | '<-' {AttributeReadSpecification})
	attribute=ParticipationAttribute
;

ParticipationAttribute:
	participationClass=[ParticipationClass|QualifiedParticipationClass] '.' attribute=[Attribute|UnqualifiedParticipationMember]
;



/**************
 * References *
 **************/
 ReferenceDeclaration:
 	'has' name=ID 'referencing' referenceType=[Metaclass|QualifiedMetaclass] '{'
 		mappings+=ReferenceMappingSpecification*
 	'}'
 ;
 
 ReferenceMappingSpecification:
 	('=' {ReferenceEqualitySpecification})
 	reference=ParticipationAttribute
 	('via' referenceTargets+=[ParticipationClass|QualifiedParticipationClass] (',' referenceTargets+=[ParticipationClass|QualifiedParticipationClass])*)?
 ;


/******************
 * Datatype Rules *
 ******************/
fragment QualifiedClasslike:
	ID ':' ID;
	
QualifiedMetaclass:
	QualifiedClasslike
;

QualifiedParticipationClass:
	QualifiedClasslike
;

DomainReference:
	ID
;

UnqualifiedMetaclass:
	ID
;

UnqualifiedParticipationMember:
	ID
;
	
	
/******************
 * Terminal Rules *
 ******************/
terminal QUOTED_NAME:
	("'" NAME "'") | ('"' NAME '"') | ("“" NAME "”") | ("»" NAME "«") | ("«" NAME "»") | ("„" NAME "“")
;

terminal fragment NAME:
	('a'..'z' | 'A'..'Z') ('a'..'Z' | 'A'..'Z' | '0'..'9' | '-' | '_')*
;
	
